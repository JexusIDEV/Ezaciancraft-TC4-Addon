buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
    }
    dependencies {
        classpath ('com.anatawa12.forge:ForgeGradle:1.2-1.1.+') {
            changing = true
        }
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'forge'

// These settings allow you to choose what version of Java you want to be compatible with. Forge 1.7.10 runs on Java 6 to 8.
sourceCompatibility = 1.8
targetCompatibility = 1.8

version = project.version
group = project.group

minecraft {
    version = project.minecraft_version + "-" + project.forge_version
    runDir = "run"
}

repositories {
    mavenCentral()

    flatDir {
        dirs 'libs'
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "Curseforge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        url "https://cursemaven.com"
    }
    maven { url "https://maven.latmod.com/" }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //for this is thaumcraft addon
    implementation("curse.maven:thaumcraft-223628:2227552")
    implementation("curse.maven:baubles-227083:2224857")

    implementation("curse.maven:tabula-minecraft-modeler-229092:2232293")
    implementation("curse.maven:ichunutil-229060:2338148")

    implementation("curse.maven:waila-73488:2417924")

    //ohhhh, my NEI
    compileOnly("codechicken:CodeChickenLib:1.7.10-1.1.3.141:dev")
    runtimeOnly("codechicken:CodeChickenLib:1.7.10-1.1.3.141:dev")
    runtimeOnly("codechicken:CodeChickenCore:1.7.10-1.0.7.48:dev")
    runtimeOnly("codechicken:NotEnoughItems:1.7.10-1.0.5.120:universal")

    //thaum nei
    implementation("curse.maven:thaumcraft-nei-plugin-225095:2241913")

    //i want to suffer
    implementation("curse.maven:avaritia-233785:2519595")
    implementation("curse.maven:howling-moon-236149:2326149")
    implementation("curse.maven:thaumic-additions-232564:2417787")
    implementation("curse.maven:thaumic-tinkerer-75598:2232793")

    //nuh nuh
}

processResources {
    // This will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // Replace values in only mcmod.info.
    filesMatching('mcmod.info') {
        // Replace version and mcversion.
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
}

// Ensures that the encoding of source files is set to UTF-8, see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// This task creates a .jar file containing the source code of this mod.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

// This task creates a .jar file containing a deobfuscated version of this mod, for other developers to use in a development environment.
task devJar(type: Jar) {
    classifier = "dev"
    from sourceSets.main.output
}

// Creates the listed artifacts on building the mod.
artifacts {
    archives sourcesJar
    archives devJar
}

// This block configures any maven publications you want to make.
publishing {
    publications {
        mavenJava(MavenPublication) {
        // Add any other artifacts here that you would like to publish!
            artifact(jar) {
                builtBy build
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(devJar) {
                builtBy devJar
            }
        }
    }

    // This block selects the repositories you want to publish to.
    repositories {
        // Add the repositories you want to publish to here.
    }
}
